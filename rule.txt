<program> ===> <otherFunctions> <mainFunction>
<mainFunction> ===> TK_MAIN <stmts> TK_END
<otherFunctions> ===> eps
<otherFunctions> ===> <function><otherFunctions>
<function> ===> TK_FUNID <input_par> <output_par> TK_SEM <stmts> TK_END
<input_par> ===> TK_INPUT TK_PARAMETER TK_LIST TK_SQL <parameter_list> TK_SQR
<output_par> ==> eps
<output_par> ===> TK_OUTPUT TK_PARAMETER TK_LIST TK_SQL <parameter_list> TK_SQR
<parameter_list> ===> <dataType> TK_ID <remaining_list>
<dataType> ===>  <constructedDatatype>
<dataType> ===> <primitiveDatatype>
<primitiveDatatype> ===> TK_REAL
<primitiveDatatype> ===> TK_INT
<constructedDatatype> ===> TK_RECORD TK_RECORDID
<remaining_list> ===> eps
<remaining_list> ===> TK_COMMA <parameter_list>
<stmts> ===> <typeDefinitions> <declarations> <otherStmts><returnStmt>
<typeDefinitions> ===> eps
<typeDefinitions> ===> <typeDefinition><typeDefinitions>
<typeDefinition> ===> TK_RECORD TK_RECORDID <fieldDefinitions> TK_ENDRECORD TK_SEM
<fieldDefinitions> ===> <fieldDefinition><fieldDefinition><moreFields>
<fieldDefinition> ===> TK_TYPE <primitiveDatatype> TK_COLON TK_FIELDID TK_SEM
<moreFields> ===> eps
<moreFields> ===> <fieldDefinition><moreFields>
<declarations> ===> eps
<declarations> ===> <declaration><declarations>
<declaration> ===> TK_TYPE <dataType> TK_COLON TK_ID <global_or_not> TK_SEM
<global_or_not> ===> eps
<global_or_not> ===> TK_COLON TK_GLOBAL
<otherStmts> ===>  eps
<otherStmts> ===> <stmt><otherStmts>
<stmt> ===> <funCallStmt>
<stmt> ===> <assignmentStmt>
<stmt> ===> <iterativeStmt>
<stmt> ===> <conditionalStmt>
<stmt> ===> <ioStmt>
<assignmentStmt> ===> <singleOrRecId> TK_ASSIGNOP <arithmeticExpression> TK_SEM
<singleOrRecId> ===> TK_ID <singleOrRecIdPrime>
<singleOrRecIdPrime> ===> eps
<singleOrRecIdPrime> ===> TK_DOT TK_FIELDID
<funCallStmt> ===> <outputParameters> TK_CALL TK_FUNID TK_WITH TK_PARAMETERS <inputParameters> TK_SEM
<outputParameters> ===> eps
<outputParameters> ===> TK_SQL <idList> TK_SQR TK_ASSIGNOP
<inputParameters> ===> TK_SQL <idList> TK_SQR
<iterativeStmt> ===> TK_WHILE TK_OP <booleanExpression> TK_CL <stmt><otherStmts> TK_ENDWHILE
<conditionalStmt> ===> TK_IF TK_OP <booleanExpression> TK_CL TK_THEN <stmt><otherStmts> <elsePart>
<elsePart> ===> TK_ENDIF
<elsePart> ===> TK_ELSE <stmt><otherStmts> TK_ENDIF
<ioStmt> ===> TK_READ TK_OP <singleOrRecId> TK_CL TK_SEM
<ioStmt> ===> TK_WRITE TK_OP <allVar> TK_CL TK_SEM
<allVar> ===> TK_ID <allVarPrime>
<allVar> ===> TK_NUM
<allVar> ===> TK_RNUM
<allVarPrime> ===> eps
<allVarPrime> ===> TK_DOT TK_FIELDID
<arithmeticExpression> ===> <term> <expPrime>
<expPrime> ===> eps
<expPrime> ===> <lowPrecedenceOperators> <term> <expPrime>
<term> ===> <factor> <termPrime>
<termPrime> ===> eps
<termPrime> ===> <highPrecedenceOperators><factor> <termPrime>
<factor> ===> <allVar>
<factor> ===> TK_OP <arithmeticExpression> TK_CL
<highPrecedenceOperators>===> TK_DIV
<highPrecedenceOperators>===> TK_MUL
<lowPrecedenceOperators> ===> TK_MINUS
<lowPrecedenceOperators> ===> TK_PLUS
<booleanExpression> ===> TK_OP <booleanExpression> TK_CL <logicalOp> TK_OP <booleanExpression> TK_CL
<booleanExpression> ===> <var> <relationalOp> <var>
<booleanExpression> ===> TK_NOT TK_OP <booleanExpression> TK_CL
<var> ===> TK_ID
<var> ===> TK_NUM
<var> ===> TK_RNUM
<logicalOp> ===> TK_OR
<logicalOp> ===> TK_AND
<relationalOp> ===> TK_LT
<relationalOp> ===> TK_LE
<relationalOp> ===> TK_EQ
<relationalOp> ===> TK_GT
<relationalOp> ===> TK_GE
<relationalOp> ===> TK_NE
<returnStmt> ===> TK_RETURN <optionalReturn> TK_SEM
<optionalReturn> ===> eps
<optionalReturn> ===> TK_SQL <idList> TK_SQR
<idList> ===> TK_ID <more_ids>
<more_ids> ===> eps
<more_ids> ===> TK_COMMA <idList>
